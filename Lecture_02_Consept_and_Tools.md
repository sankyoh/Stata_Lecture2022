# 今日のゴール
* 効率的なStataプログラミングのためのコンセプトを理解する。
* 効率的なStataプログラミングのためのツールを使う。 
* Project fileとmaster.doファイルを作成する。

# 今日取り扱う内容
* Current working directoryとadoファイルのパス
* Project fileとmaster.doファイル
* do-file editor
* 変数・マクロ・マトリクス・フレーム

# カレントディレクトリとシステムディレクトリ
## カレントディレクトリ
Stataでは（多くの他のアプリケーションと同様に）、Current working directory（フォルダ）という概念があります。StataはこのCWDにあるファイルに注目しています。Stata画面上では左下に常に表示されています。また`pwd`コマンドでも表示させることができます。

CWDを把握しておくことは重要です。まず、指定をしなければ、ファイル利用はCWDから行なわれます。例えば、`save data, replace`では、CWDに保存されますし、`use data, clear`はCwDから**data.dta**を探します。また、ファイルやディレクトリの位置を相対パスで指定するときは、CWDからの位置として表現されます（下表）。


| パスの書き方  | 意味 |
| ------------- | ------------- |
| ./  | CWD  |
| ./data  | CWDにあるdataディレクトリ  |
| ../  | CWDの1つ上のディレクトリ  |
| ../document  | CWDの1つ上のディレクトリにあるdocumentディレクトリ  |
| ../../  | CWDの2つ上のディレクトリ  |

## システムディレクトリ
`adopath`を実行することで、ado-fileが保存されているディレクトリを表示させることができます。Stataがadoファイルを検索するディレクトリが示されます。優先度は1～6の順になっていますので、仮に同名のado-fileが複数のディレクトリに格納されていた場合は、優先度が高いディレクトリに入っているado-fileが優先されます。また、6(oldplace)まで探してもado-fileが存在しない場合は、**unrecognized error**が発生します。

## その他の関連コマンド
| コマンド  | 説明 |
| ------------- | ------------- |
| pwd| CWDを表示する|
| cd  | ディレクトリを移動する |
| mkdir | ディレクトリを作成する  |
| sysdir |adopathの類似|

# Project managerとmaster.doファイル
この講義では、do-fileを1ファイルに統一せずに、解析や操作の目的・コンセプトごとにdo-fileを分けることを強く推奨しています。

そのため、解析規模が大きくなると、Data-fileやdo-fileの個数が多くなってしまうことがあります。そのような場合（そのような場合で無かったとしても？）には、Progect managerを活用します。

Project managerは、Stata13から実装されたシステムです。Project managerはプロジェクトに関連するすべてのファイルを1つのインターフェイスに集約し、素早くアクセスできるようにします。Project managerでファイルをダブルクリックすると、do-fileをdo-file editorで開いたり、Dataファイルを使用したり、保存したStataグラフを表示したりすることができます。プロジェクトに追加できるファイルの種類に制限はありません。

## Project fileの作り方
Projectは、Project file（.stpr）で管理されます。Stataで解析をする際には、下記の手順で、Stata Project fileを作るところから作業を始めて下さい。

Windows/Unix（プロジェクトファイル作成メニューがdo-file editorにある）
1. Windows/Unix上でプロジェクトディレクトリを作成する。
2. Stataを起動する
3. `cd`コマンドでプロジェクトディレクトリに移行する
4. `doedit`コマンドでdo-file editorを起動する
5. メニューバーから、プロジェクトファイルを作成する。

Mac（プロジェクトファイル作成メニューがメインメニューにある）
1. Mac上でプロジェクトディレクトリを作成する。
2. Stataを起動する
3. `cd`コマンドでプロジェクトディレクトリに移行する
4. メニューバーから、プロジェクトファイルを作成する。

## Project fileの使い方
[Stata Quick Tip: Project Manager](https://www.youtube.com/watch?v=ppw_Z0qH3q8)
講義では、実際に操作してみようと思いますが、上記のStata社の動画でも良いと思います。

## master.doファイル
do-fileを複数作成した場合、それらの相互関係や動作順についても記述しておく必要があります。その目的でmaster.doというファイルを作っています。

master.doの例
```stata
* 1. Import Data (Excel to DTA)
do crDataset       // データ読み込み
do crPropScore     // 傾向スコア算出
do crSanChk        // データチェック

* 2. Des Stat
do anDesStata // Table 1
do anBalChk   // IPTW後のバランスチェック作表
do anGraph    // グラフ描画

* 3. Logistic regression
do anLogistic crude    // 粗解析モデル
do anLogistic adjusted // 調整モデル

* 4. Sub-group analysis
forvalues x=1/3 {
  do anLogistic crude    subgroup`x'
  do anLogistic adjusted subgroup`x'
}

// paste_Docx.do  // グラフをワードファイルに貼るanGraphから繰り返し呼び出し。
```

このmaster.doでは、Excelファイルからデータを読み込み、解析結果を実行するまでの一連の流れを示しています。サンプルなので、コメント欄記載がやや薄いですが、実データであればより丁寧に記載します。

個人的には最初にこのmaster.doを最初に作り、後で中身のdo-fileを作っていきます。途中で変更もあったり、採用するdoファイルの変更がありますが、それはそれとして、最初に設定した解析のゴールを見えるようにします。

この例では、`anLogistic.do`と`paste_Docx.do`を繰り返し使用して作業の効率化を図っています（プロジェクト内の再利用）。 また、IPTW後のanBalChkも他のデータセットでも少しの修正で容易に再利用可能です（プロジェクト外への再利用）。

## 相対パス指定を優先する
Project file内部では、出来るだけ相対パス指定を行ないます。

例えば、Dataファイルは、do-fileから`use`コマンド等を利用して読込みを行ないます。絶対パスはいろいろな事情で変化する場合があります。特に外部ストレージに保存している場合には、ドライブレター（Cドライブ、Dドライブ等）が容易に変化するため、StataがDataファイルを見つける事ができず、エラーが発生します。

そのため、相対パス指定を行なうことによって、エラー発生確率を下げておきます。

```stata
use "D:Data/stata_proj/Dataset.dta", clear // 絶対パス指定
use Dataset.dta, clear // 相対パス指定
```

# do-fileの効率的な編集
## 編集ツール
Stata付属のdo-file editorを利用するのが、Stataにおいては最も効率的な作業ができると思います。

また、やや古いドキュメントですが、下記のような紹介もあります。
[Some notes on text editors for Stata users](http://fmwww.bc.edu/RePEc/bocode/t/textEditors.html) by Nicolas J Cox

## do-file editorの特徴
### キーワード色変化
コマンドや関数を打ち込むと色が変化します。これによって、打ち間違いを避けることができます。変化が分かり難い色があれば、変更すると良いと思います（私はコマンドを薄紫にしています）。

### サジェストからの補完
コマンドなどを途中まで打ち込むと、候補がサジェストされます。サジェストを決定するときは、Tabキーで選択・補完することができます。ただし、日本語コメントを打ち込んでいるときには変なサジェストがでてくるので、やや使いにくい点があります。

doファイル中に登場していない変数はサジェストされないので、最初に変数名をコピーし、do-fileの最後とかの外れた場所にペーストすることでサジェストが出来るようになります。変数名コピーは、メインウィンドウで行ないます。

### ブロックの折り畳み
繰り返し処理（`foreach`や`forvalues`など）や`program define`などの一連の塊を折りたたむことができます。

### ブックマーク作成・ブックマーク間移動
行番号のすぐ右側をクリックするか、Cntl+F2でブックマークを挿入することができます。このブックマークは通常のコメントとはことなり、Shift+F2やF2でブックマークへの移動ができます。ただし、この機能が必要になるような長いdo-fileの作成は避けた方が良いと思います。

この機能は、どちらかというとado-file作成時に活用できると思います。

# Stataで扱うデータ（変数・マクロ・マトリクス・フレーム）
## 変数 variable
Dataファイルで読み込まれ、メインウィンドウの右上に表示されている数や文字列です。Stataで解析を行なう場合のほとんどは、この変数を操作しています。

変数には幾つかの型があります。大きく二分すると、「数値」と「文字列」になりますが、「数値」はさらに細かく分かれています。

| 型  | 最小値 | 最大値 | バイト |
| ------------- | ------------- | ------------- | ------------- |
| byte| $-127$ | $100$ | 1 |
| int  | $-32,767$ | $32,700$ | 2 |
| long | $-2,147,483,647$ | $2,147,483,620$ | 4 |
| float | $-1.701×10^{38}$ | $1.701×10^{38}$ | 4 |
| double | $-8.988×10^{307}$ | $8.988×10^{307}$ | 8 |

byte、int、longの3種類が整数を格納し、floatとdoubleは小数点下の情報も格納します（浮動小数）。

`compress`コマンドを実行すると、各変数に対して適切な（最もバイト数が小さくて済む）型に直してくれます。

浮動小数型については注意が必要です。下記で例をしまします。

```stata
clear
input x y
1 1.1
2 1.2
3 1.3
end

list
```
`input`コマンドで変数xと変数yにデータを入力し、`list`コマンドで表示しています。明らかに、y=1.1というデータが1個あることに注目して下さい。

```stata
count if x==1
count if y==1.1
```

`count`コマンドは、条件に合致する観察数を返すコマンドです。`count if x==1`では、「1」を返しますが、`count if y==1.1`では、「0」を返しています。しかし、`list`コマンドで見たように、y=1.1という観察があることは明らかです。Stataは間違ってしまったのでしょうか？

Stataでfloatとdoubleは（というかコンピューター全般）、数値を二進数で格納しています。意外なことかも知れませんが、1.1に相当する二進数は存在しません（循環小数になります）。

| 10進数 | 2進数 | 
| ------------- | ------------- |
| 1.1 | 1.000110011... |

また、Stataでは、数値は計算や評価の前に一度double型に直してから計算するという性質があります。後に示すように変数yはfloat型ですが、1.1はdouble型として扱われます。そのため、`if y==1.1`をより厳密に書くと、「float型のyとdouble型の1.1は等しいか？」という比較を行なっています。double型の1.1とfloat型の1.1は、いずれも循環小数ですが、記述できる小数点下の桁数が異なっているので、値としても微妙に異なっています（ $2.384×10^{-8}$ くらいの微妙な差）。

このような場合、下記のような対応を行ないます。

```stata
des 

count if y==1.1         // 右辺はdouble型
count if y==float(1.1)  // 右辺はfloat型
```

`des`で確認できるように、変数yはfloat型です。`if節`の右辺がdouble型になり、比較が正しく行えませんので、`float関数`を用いて、右辺もfloat型に直します。

これで、想定通りの動きになっています。

このような変数型により問題は、Excelファイルを読み込むときにも生じます。Excel関数で計算された値が小数を含んでいるときにはよく注意して下さい。可能であれば、Stata内で計算結果を変数にするほうがエラーは少ないと思います。

初級では、変数の型は「数値」と「文字列」の区別で十分ですが、よくわからないエラーに対応するためには、「float型」と「double型」の違いにも注意が必要です。

## マクロ Macro
### マクロの基本
Stataにおけるマクロはの数値を仮置きしたり、文字列をマクロに格納することで後から再利用したりするための、変数（解析対象としているデータセットの変数とは異なった存在）です。Excelなどにおけるマクロとは全く異なる意味を持った用語なので、混同しないようにしてください。

マクロには、マクロ名とマクロコンテンツから構成されます。

コマンドの中で「マクロ名」が現れると、マクロ名はマクロコンテンツに置き換えられます。

```stata
local macro_name contents
display "`macro_name'"
```

2行目の`display`コマンドが実行時に、``マクロ名：`macro_name'``は、`contents`に置き換わります。そのため、画面には「contents」と表示されます。

マクロ名の中にマクロ名を入れ込む事も可能です。

```stata
local var_name1 bmi
local var_name2 sbp
local var_name3 dbp
local i=1
gen `var_name`i'' = weight/height^2
```

5行目の`` マクロ名：`var_name`i'' ``は、入れ子構造になっています。下記の様にStataは解釈します。
1. 内側の``マクロ名：`i'``をコンテンツ（つまり、「1」）に置き換える。
2. 置き換えると、``マクロ名：`var_name1'``になる。
3. ``マクロ名：`var_name1'``をコンテンツ（つまり、「bmi」）に置き換える。
4. `gen bmi = weight/height^2`が実行される。

### グローバルマクロとローカルマクロ
マクロには、グローバルとローカルの2種類があります。マクロ名には、グローバルマクロは32文字まで、ローカルマクロは31文字までの長さ制限があります。グローバルマクロは`global`コマンドで、ローカルマクロは`local`コマンドでそれぞれ定義します。

グローバルマクロは一度定義すれば、Stataのどこででも利用することができます。グローバルマクロのマクロ名には先頭に「$」を付けます。

ローカルマクロは定義されたprogramまたはdo-file内にのみ存在します。ローカルマクロのマクロ名は「`」と「'」で挟みます。

入れ子構造を明確にしたり、誤動作を避けるために中括弧`{ }`を使います。

```stata
global b1 global
local b1 local
local i=1
display "$b`i'"
display "${b`i'}"
display "`b`i''
```

4行目では、入れ子構造はありません。そのため、``マクロ名：$b``と``マクロ名：`i'``をそれぞれコンテンツに置き換えます。グローバルマクロ$bは定義されていませんので、空白になります。ローカルマクロ`` `i' ``は、「1」に置き換えられます。コマンドは`display "1"'と解釈され、「1」が表示されます。

5行目では、中括弧の中から置き換えを行ないます。``マクロ名：`i'``を「1」に置き換えます。その結果、`display "$b1"'と解釈されます。`マクロ名：$b1`は、「global」に置き換えられますので、コマンドは`display "global"`となります。中括弧を使用するかどうかで入れ子構造を異なった挙動となるため、注意が必要です。

6行目は先に説明したローカルマクロの入れ子です。なお、ここで見たように、グローバルマクロとローカルマクロで同名にしてもエラーにはなりません（**人間が混乱するだけです**）。

### ローカルマクロの有効範囲

ローカルマクロは、ローカルマクロを定義したprogramまたはdo-fileが、他のprogramまたはdo-fileを呼び出すと、それまで定義されていたローカルマクロは一時的に無くなります。呼び出し元のプログラムに戻ったら、ローカルマクロは復活します。

```stata
local x "x outside"

capture program drop tmp
program define tmp
  local y "y inside"
  display "`x'"
  display "`y'"
end

tmp

display "`x'"
display "`y'"
```

1行目でローカルマクロxをプログラムの外で定義し、5行目でローカルマクロyを定義しています。

プログラムtmp内の出力コマンドでは、6行目の出力では何も出力されません。これは、ローカルマクロxは、プログラムの外で定義されているため、プログラム内では使えないためです。一方で、最終行の出力では何も出力されません。これはローカルマクロyがプログラム内で定義されているため、プログラム外では利用できないためです。

また、programやdo-fileの実行が終わると、ローカルマクロは永久に削除されます。

### QUIZ
下記のコードでは、何が起こるでしょうか？
```stata
local x "x outside"
local y "y outside"

capture program drop tmp
program define tmp
  local x "x inside"
  local y "y inside"
  display "`x'"
  display "`y'"
end

tmp

display "`x'"
display "`y'"
```

## マトリクス（行列） Matrix
Stataでは、2つの方法で行列を取り扱うことができます。1つは前回にも登場したMataという名前の言語を使う方法です（`help meta`）。この方法は高速に負荷が高い計算を行うことができるので有用ですが、学習には労力が必要です。今回のコースでは、高速計算を意図してはいないので、そうではない方法（`help matrix`）を用います。

行列計算を行うことが出来れば、様々な統計解析手法をスクラッチで組むこともできますが、今回のコースでは、解析結果を上手く取り扱う（再利用）ために、行列を使います。

詳細は今後の講義で紹介しますが、簡単な例を挙げておきます。

```stata
sysuse auto, clear
regress mpg weight price turn
return list
matlist  r(table)
mat list r(table)
ereturn list
```

1行目で、サンプルデータを読み込み[^1]、2行目で、アウトカム=燃費（mpg）で、曝露=車重（weight）、交絡=値段（price）と旋回半径（turn）とした重回帰分析を行ないました。

3行目の`return list`が見慣れませんが、これを実行すると、regressコマンドで得られた結果（一部）が得られます。ここでは`r(table)`とのみ出ています。

この中身をみているのが4行目と5行目のコマンドです。regressコマンドの結果として表示されているものの一部がここで見ることができます。マトリクスは単に見るだけでは無く、中身をうまく取得したり、操作したりすることで、好みの出力を得ることを目指します。

なお、ereturn listでは他の結果が取得できます。

## フレーム Frame
長らく、他の統計ソフト（RやPyton）に比べて、Stataの弱点は、**同時に1つのデータセットしか取り扱うことができない**という事でした。これをかなりの部分で解消したのがフレーム（`help frame`）という概念です。

フレーム（Python風にいうと、データフレーム）によって、複数のデータセットをメモリに同時に格納することができます。 メモリ内のデータセットはフレームに格納され、Stataでは複数のフレームを使用することができます。

複数のフレームを使う利点にはいくつかあります。

* 複数のデータセットを相互に操作しながら、1つのデータセットにまとめる。
* シミュレーションで得られた統計情報をフレームに記録。
* 処理の高速化：フレームはディスク（SSDやHDD）ではなく、メモリ上に展開されるので、処理が早い。

フレームを使った操作についても、今後の講義で紹介します。

[^1]: 親の顔よりよく見たサンプルデータ。
